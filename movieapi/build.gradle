plugins {
    id 'com.android.library'
    id 'kotlin-android'
}
apply from: '../dependencies.gradle'

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdkVersion versions.sdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.sdk
        versionCode versions.versionCode
        versionName versions.versionName

        buildConfigField "String", "API_KEY", "\"${getProperty("gradle.properties", "API_KEY")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "$versions.jvmTarget"
    }
}

dependencies {
    implementation project(":metadata")

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"

    //test
    testImplementation "junit:junit:$versions.junit"
}